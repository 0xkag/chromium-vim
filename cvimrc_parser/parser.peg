{
  function merge(a, b) {
    for (var p in b) {
      if (typeof b[p] === 'object' && !Array.isArray(b[p])) {
        if (!a.hasOwnProperty(p)) {
          a[p] = {};
        }
        merge(a[p], b[p]);
      } else {
        a[p] = b[p];
      }
    }
  }
  function parseScope(data) {
    var result = {MAPPINGS: []};
    data.forEach(function(e) {
      if (e.MAPPINGS) {
        result.MAPPINGS.push(e.MAPPINGS);
      } else {
        merge(result, e);
      }
    });
    result.MAPPINGS = result.MAPPINGS.join('\n');
    return result;
  }
  function objectPlural(key) {
    var replacements = {
      qmark: 's',
      searchengine: 's',
      searchalias: 'es'
    };
    if (replacements.hasOwnProperty(key))
      key += replacements[key];
    return key;
  }
}

Start
  = a:Scope { return a; }

EOF = !.
EOL = [\n\r]
WhiteSpace = [ \t]
__ = (EOL / WhiteSpace)*
MapIdentifier = a:(!WhiteSpace !EOL .)+ { return text(); }
SEPVAL
  = WhiteSpace* EOL+ WhiteSpace* "\\" WhiteSpace* { return null; }
  / WhiteSpace+ { return null; }
SEP
  = SEPVAL+

Identifier = [a-zA-Z_$][a-zA-Z_$0-9]* { return text(); }

LET    = 'let'
SET    = 'set'
SITE   = 'site'
CALL   = 'call'
MAP    = 'map'
IMAP   = 'imap'
UNMAP  = 'unmap'
IUNMAP = 'iunmap'

StringCharacter
  = '\\' a:[^\n] { return a; }
  / [^\n]
  / SEP { return ''; }

DoubleStringCharacter
  = !'"' a:StringCharacter { return a; }
SingleStringCharacter
  = !'\'' a:StringCharacter { return a; }

StringLiteral
  = '"' a:DoubleStringCharacter* '"' { return a.join(''); }
  / '\'' a:SingleStringCharacter* '\'' { return a.join(''); }

IntegerLiteral
  = '0' { return 0; }
  / [1-9][0-9]* { return parseInt(text(), 10); }

ArrayList
  = SEP? a:Expression SEP? ',' b:ArrayList SEP? { return [a].concat(b); }
  / SEP? a:Expression SEP? { return a; }

ArrayLiteral
  = '[' a:ArrayList ']' { return a; }
  / '[' SEP? ']' { return []; }

Expression
  = StringLiteral
  / IntegerLiteral
  / ArrayLiteral

LetStatement
  = LET SEP a:Identifier SEP? '=' SEP? b:Expression {
    var r = {}; r[a] = b; return r;
  }
  / LET SEP a:Identifier SEP b:Identifier SEP? '=' SEP? c:Expression {
    a = objectPlural(a);
    var r = {}; r[a] = {}; r[a][b] = c; return r;
  }

SetStatement
  = SET SEP 'no' a:Identifier { var r = {}; r[a] = false; return r; }
  / SET SEP !'no' a:Identifier { var r = {}; r[a] = true; return r; }

SiteStatement
  = SITE SEP a:StringLiteral SEP? '{' WhiteSpace* EOL b:Scope '}' {
    var r = {sites: {}}; r.sites[a] = b; return r;
  }

UnmapStatement
  = a:(UNMAP / IUNMAP) SEP b:MapIdentifier {
    return { MAPPINGS: a + ' ' + b };
  }

MapStatement
  = a:(MAP / IMAP) SEP b:MapIdentifier SEP c:[^\n]* {
    return { MAPPINGS: [a, b, c.join('')].join(' ') };
  }

CallStatement
  = CALL SEP a:[^\n]* {
    return { MAPPINGS: 'call ' + a.join('') };
  }

StatementValue
  = LetStatement
  / SetStatement
  / SiteStatement
  / ('unmapAll' / 'iunmapAll') {
    return {MAPPINGS: text() };
  }
  / CallStatement
  / MapStatement
  / UnmapStatement

Statement
  = __ a:StatementValue WhiteSpace* (EOF / EOL __) { return a; }

Scope
  = a:Statement* { return parseScope(a); }
